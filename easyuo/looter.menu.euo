; Looter-Menu v3.0.0 2014-10-19T18:10:21-02:00
; more on github.com/haggen/euo
;
; About:
; Provides a graphic menu to select the loot table.
;
; Depends on:
; - Looter v2 [github.com/haggen/euo/Looter.euo]
; - Library v1.2 [github.com/haggen/euo/Library.euo]
;
; Roadmap:
; - Allow user to clear ignore list
;

; Make it fast
linespercycle 60

; Turn on/off debug messages
set %debug #false

; Define correct namespace
if #nsname <> Looter
{
  namespace push
  namespace global Looter
}

; Check for refresh flag and skip build part
if refresh in %1
  goto refresh

; Build the menu
build:
  menu window title Looter
  menu window size 290 455

  menu text label_13 10 10 Operation mode:

  menu combo create option_13 10 30 270
  menu combo add option_13 Loot automatically
  menu combo add option_13 Wait for hotkey
  menu combo select option_13 2

  menu text label_14 10 65 Hotkey modifier and key:
  menu edit option_14 10 85 100 CTRL
  menu edit option_15 115 85 165 A

  menu check option_1 10 125 270 20 #true Gold
  set %option_1 POF_

  menu check option_2 10 145 270 20 #true Hide, leather and feathers
  set %option_2 DEG_JJG_

  menu check option_3 10 165 270 20 #true Magery reagents
  set %option_3 KZF_SZF_MZF_JUF_RZF_KUF_WZF_

  menu check option_4 10 185 270 20 #true Gems and Arcane Gem
  set %option_4 EVF_GVF_ZVF_BVF_RVF_HVF_VVF_JSL_UVF_NVF_

  menu check option_5 10 205 270 20 #true Musical instruments
  set %option_5 OQF_LQF_PRF_KRF_MQF_QRF_RGP_PGP_

  menu check option_6 10 225 270 20 #true Blades and polearms
  set %option_6 FUO_ATF_NPO_JPH_TSF_NMH_VRH_JTF_SOH_BPH_HSF_ZTO_BUO_XTH_OPO_CUO_XRH_GUO_MTF_RRH_

  menu check option_7 10 245 270 20 #true Blunt weapons and axes
  set %option_7 LSF_BSF_NSF_ZRF_RMH_LPH_UOH_VTH_YSF_BUH_AUO_TRH_ZTH_

  menu check option_8 10 265 270 20 #true Bows, crossbows and ammunition
  set %option_8 WOH_TOH_QPO_SPO_WOH_TOH_JSF_USF_LMH_MMH_QPO_SPO_PPO_RPO_JSF_USF_LMH_MMH_PPO_RPO_JSF_RWF_LNK_FKF_

  menu check option_9 10 285 270 20 #true Leather armor
  set %option_9 VKH_NJL_QJL_JKH_QKH_DLH_HKH_PKH_SKH_KKH_ELH_PSF_QSK_ATK_ZSK_OSK_YSK_NSK_ALH_ULH_LLH_YKH_GLH_XKH_FLH_RLH_MLH_SSK_RSK_

  menu check option_10 10 305 270 20 #true Platemail
  set %option_10 MSH_LSH_NSH_ISH_HSH_KSH_JSH_USH_WSH_ASH_ESH_GSH_OSH_CSH_

  menu check option_11 10 325 270 20 #true Shields
  set %option_11 FIK_GIK_CIK_NIK_AIK_MIK_HLK_CLK_ZHK_BIK_LIK_OIK_BLK_ELK_DLK_KCO_

  menu check option_12 10 345 270 20 #false Containers (bags, backpackes, etc.)
  set %option_12 , ZJF_LKF_CKF_YKK_

  menu button option_16 10 375 270 30 Retarget lootsack

  menu button option_17 10 415 270 30 Clear ignore list

  menu show

  exit

; Refresh loot every half second
refresh:
  set %_loot

  for %i 1 12
  {
    menu get option_ . %i
    if #menures
      set %_loot %_loot , %option_ . %i
  }

  set !loot %_loot
  
  menu get option_13
  if #menures = 1
    set !mode auto
  else
    set !mode hotkey
    
  menu get option_14
  set !hotkey_m #menures

  menu get option_15
  set !hotkey_k #menures

  if #menubutton = option_16
  {
    set *lootsack
    set #menubutton none
  }

  if #menubutton = option_17
  {
    ignoreitem reset
    set #menubutton none
  }

  exit
