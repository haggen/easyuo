; Autoloot v1.0.0
; by haggen <arthur@corenzan.com>
;
; Roadmap
; - beware of your weight
; - check if player is lifting something

; Speed things up
set #lpc 50

; Comment out the loot you don't want.
; Gold
set %loot POF_
; Magery reagents
set %loot %loot , KZF_SZF_MZF_JUF_RZF_KUF_WZF_
; Gems, inclusing Arcane Gem
set %loot %loot , EVF_GVF_ZVF_BVF_RVF_HVF_VVF_JSL_UVF_NVF_
; Bags, backpacks and other containers
set %loot %loot , ZJF_LKF_CKF_YKK_
; Swords and axes
set %loot %loot , YPO_BNF_CPH_INF_FSF_OSF_ASF_HNF_FMH_CNF_LSF_NSF_ISF_SMH_RMH_CSF_ZRF_MPH_LPH_MSF_BSF_KPO_GUO_KTF_JTF_BPH_EPH_ZSF_ATF_JPH_SOH_POH_NMH_OMH_LPO_ZTO_XTH_YTH_HSF_KSF_NPO_XPO_FUO_JPO_QPF_NPF_KPH_FYG_RHM_SFR_
; Bows, crossbows and ammunition
set %loot %loot , WOH_TOH_QPO_SPO_WOH_TOH_JSF_USF_LMH_MMH_QPO_SPO_PPO_RPO_JSF_USF_LMH_MMH_PPO_RPO_JSF_RWF_LNK_FKF_
; Leather and studded leather armors
set %loot %loot , VKH_NJL_QJL_JKH_QKH_DLH_HKH_PKH_SKH_KKH_ELH_PSF_QSK_ATK_ZSK_OSK_YSK_NSK_
set %loot %loot , ALH_ULH_LLH_YKH_GLH_XKH_FLH_RLH_MLH_SSK_RSK_
; Platemail
set %loot %loot , MSH_LSH_NSH_ISH_HSH_KSH_JSH_USH_WSH_ASH_ESH_GSH_OSH_
; Shields
set %loot %loot , LIK_MIK_CLK_AIK_CIK_LYD_GIK_NIK_ZHK_HLK_BLK_FIK_BIK_OIK_
; Musical instruments
set %loot %loot , OQF_LQF_PRF_KRF_MQF_QRF_RGP_PGP_

; Define loot bag
finditem *lootbag
if #findcnt > 0
  goto search

event exmsg #charid 0 28 Target loot bag
set #targcurs 1
loop:
  if #targcurs = 1
    goto loop
set *lootbag #ltargetid

; Look for corpses up to 2 tiles away
search:
  finditem YFM_WNF_QNF g_2

  if #findcnt = 0
    goto search

  set %corpse #findid
  set %started_at #scnt
    
  event exmsg %corpse 0 28 Corpse

  set #nextcposx 50
  set #nextcposy 50

  ; Open the corpse
  set %j #jindex
  set #lobjectid %corpse
  event macro 17
  gosub wait 6
  
  ; Check if we can loot the corpse
  gosub journal %j criminal_act
  if #result
  {
    event exmsg %corpse 0 28 Nope
    ignoreitem %corpse
    goto search
  }

  ; If the container size does not match
  ; a corpse container, look again
  if #contsize <> 144_212
      goto search

  ; Search for loot items
  finditem %loot c_ , %corpse
  
  if #findcnt = 0
  {
    event exmsg %corpse 0 28 Empty
    ignoreitem %corpse
    goto search
  }
  
  while #findindex <= #findcnt
  {
    event sysmessage #findindex , / , #findcnt
    set %j #jindex
    exevent drag #findid #findstack
    exevent dropc *lootbag
    gosub wait 3
    
    gosub journal %j must_wait_to_perform
    if #result
      continue
      
    gosub journal %j too_far_away
    if #result
      goto search

    gosub journal %j out_of_sight
    if #result
      goto search

    event property #findid
    event exmsg #charid 0 28 #property
    
    set #findindex #findindex + 1
  }
  
  set %time #scnt - %started_at
  event exmsg #charid 0 28 Acquired #findcnt item(s) in %time sec
  goto search
  
  ; bvidler1@binghamton.edu
  
sub wait
  set %t #scnt2 + %1
  repeat
  {
    wait 1
  }
  until #scnt2 >= %t
  return
  
sub journal
  if %0 < 3
  {
    set %3 %2
    set %2 #jindex
  }
  for %i %1 %2
  {
    scanjournal %i
    if %3 in #journal
      return #true
  }
  return #false
  
