; Looter-Menu v3.0.0 2014-10-19T18:10:21-02:00
; more on github.com/haggen/euo
;
; About:
; Provides a graphic menu to select the loot table.
;
; Depends on:
; - Looter v2 [github.com/haggen/euo/Looter.euo]
; - Library v1.2 [github.com/haggen/euo/Library.euo]
;
; Roadmap:
; - Allow user to clear ignore list
;

; Make it fast
linespercycle 80

; Turn on/off debug messages
set %debug #false

; Define correct namespace
if #nsname <> Looter
{
  namespace push
  namespace global Looter
}

; Check for refresh flag and skip build part
if refresh in %1
  goto refresh

; Build the menu
build:
  menu window title Looter
  menu window size 290 395

  menu text label_12 10 10 Operation mode:

  menu combo create option_12 10 30 270
  menu combo add option_12 Loot automatically
  menu combo add option_12 Wait for hotkey
  menu combo select option_12 2

  menu text label_13 10 65 Hotkey modifier and key:
  menu edit option_13 10 85 100 CTRL
  menu edit option_14 115 85 165 A

  menu check option_1 10 125 270 20 #true Gold
  set %option_1 POF_

  menu check option_2 10 145 270 20 #false Magery reagents
  set %option_2 KZF_SZF_MZF_JUF_RZF_KUF_WZF_

  menu check option_3 10 165 270 20 #false Gems and Arcane Gem
  set %option_3 EVF_GVF_ZVF_BVF_RVF_HVF_VVF_JSL_UVF_NVF_

  menu check option_4 10 185 270 20 #false Musical instruments
  set %option_4 OQF_LQF_PRF_KRF_MQF_QRF_RGP_PGP_

  menu check option_5 10 205 270 20 #false Swords and axes
  set %option_5 YPO_BNF_CPH_INF_FSF_OSF_ASF_HNF_FMH_CNF_LSF_NSF_ISF_SMH_RMH_CSF_ZRF_MPH_LPH_MSF_BSF_KPO_GUO_KTF_JTF_BPH_EPH_ZSF_  ATF_JPH_SOH_POH_NMH_OMH_LPO_ZTO_XTH_YTH_HSF_KSF_NPO_XPO_FUO_JPO_QPF_NPF_KPH_FYG_RHM_SFR_

  menu check option_6 10 225 270 20 #false Bows, crossbows and ammunition
  set %option_6 WOH_TOH_QPO_SPO_WOH_TOH_JSF_USF_LMH_MMH_QPO_SPO_PPO_RPO_JSF_USF_LMH_MMH_PPO_RPO_JSF_RWF_LNK_FKF_

  menu check option_7 10 245 270 20 #false Leather armor
  set %option_7 VKH_NJL_QJL_JKH_QKH_DLH_HKH_PKH_SKH_KKH_ELH_PSF_QSK_ATK_ZSK_OSK_YSK_NSK_

  menu check option_8 10 265 270 20 #false Studded leather armor
  set %option_8 ALH_ULH_LLH_YKH_GLH_XKH_FLH_RLH_MLH_SSK_RSK_

  menu check option_9 10 285 270 20 #false Platemail
  set %option_9 MSH_LSH_NSH_ISH_HSH_KSH_JSH_USH_WSH_ASH_ESH_GSH_OSH_CSH_

  menu check option_10 10 305 270 20 #false Shields
  set %option_10 LIK_MIK_CLK_AIK_CIK_LYD_GIK_NIK_ZHK_HLK_BLK_FIK_BIK_OIK_

  menu check option_11 10 325 270 20 #false Containers (bags, backpackes, etc.)
  set %option_11 , ZJF_LKF_CKF_YKK_

  menu button option_15 10 355 270 30 Retarget lootsack

  menu show

  exit

; Refresh loot every half second
refresh:
  set %_loot

  for !i 1 11
  {
    menu get option_ . !i
    if #menures
      set %_loot %_loot , %option_ . !i
  }

  set !loot %_loot
  
  menu get option_12
  if #menures = 1
    set !mode auto
  else
    set !mode hotkey
    
  menu get option_13
  set !hotkey_m #menures

  menu get option_14
  set !hotkey_k #menures

  if #menubutton = option_15
  {
    set *lootsack
    set #menubutton N/A
  }

  exit
