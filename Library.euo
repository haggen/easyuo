; Library v1.0.1 2014-10-15 23:12:20 -0300
; more on github.com/haggen/euo
;
; About:
; Commonly used subroutines are deferred to this file.
;
; Usage:
; call Library.euo <subroutine> <arguments...>
;

; Define namespace
if #nsname <> Library
{
  namespace push
  namespace local Library
}

; Call given subroutine name, with
; following optional arguments.
set !args

if %0 > 1
{
  for %i 2 %0
    set !args % . %i , #spc , !args
}

gosub %1 !args
exit

;
; Wait, in tenths of a second.
;
; %1 -> time to wait
;
sub wait
  set !w #scnt2 + %1
  repeat
  {
    wait 1
  }
  until #scnt2 >= !w
  return

;
; Reads journal between two points, if third
; argument is omitted assumes last index and
; read string from second argument.
;
; %1 -> starting index
; %2 -> ending index
; %3 -> string to search for
;
sub journal
  if %0 < 3
  {
    set %3 %2
    set %2 #jindex
  }
  for !i %1 %2
  {
    scanjournal !i
    if %3 in #journal
      return #true
  }
  return #false

;
; Mark or reckon time passed
; since last call, in seconds.
;
sub benchmark
  set !r #scnt - !b
  set !b #scnt
  return !r

halt
