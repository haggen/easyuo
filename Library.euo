; Library v1.2.0 2014-10-18T18:01:10-03:00
; more on github.com/haggen/euo
;
; About:
; Commonly used subroutines are deferred to this file.
;
; Usage:
; call Library.euo <subroutine> <arguments...>
;

; Make it fast
linespercycle 80

; Define namespace
if #nsname <> Library
{
  namespace push
  namespace local Library
}

; Call given subroutine name, with
; following optional arguments.
set !a
set !0 %0 - 1

if %0 > 1
{
  for !i 2 %0
  {
    set !h !i - 1
    set ! . !h % . !i
    set !a !a , #spc , % . !i
  }
}

; Call selected subroutine
gosub %1 !a

; Load back the previous namespace
namespace pop

; Done
exit

;
; Reads journal between two points, if third
; argument is omitted assumes last index and
; read string from second argument.
;
; !1 -> starting index
; !2 -> ending index
; !3 -> string to search for
;
sub journal
  if !0 < 3
  {
    set !3 !2
    set !2 #jindex
  }
  for !i !1 !2
  {
    scanjournal !i
    if !3 in #journal
      return #true
  }
  return #false

;
; Mark or reckon time passed
; since last call, in seconds.
;
sub benchmark
  set !r #scnt - !b
  set !b #scnt
  return !r

;
; Wait for player to target something.
;
sub target
  set #targcurs 1
  while #targcurs = 1
    wait 2
  return #ltargetid

;
; Display debug message if %debug flag is #true
;
sub debug
  if %debug
    event sysmessage !a
  return

;
; Move one item to given container, 
; and save possible response messages
; into the result. It does not scan for
; items though.
;
; !1 -> id of the item you want to move
; !2 -> id of the container where the item will be moved into
; !3 -> pause time after dropping the item, default is 6 (cycles, i.e. 300ms)
;
; TODO: explain return messages
;
sub move
  set !j #jindex

  if !0 < 3
    set !3 6

  exevent drag !1 65535
  exevent dropc !2
  wait !3

  ; Check for possible response messages
  call Library.euo journal !j must_wait_to_perform
  if #result
    return must_wait_to_perform

  call Library.euo journal !j am_dead
  if #result
    return am_dead

  call Library.euo journal !j cannot_do_that
  if #result
    return cannot_do_that

  call Library.euo journal !j too_far_away
  if #result
    return too_far_away

  call Library.euo journal !j out_of_sight
  if #result
    return out_of_sight

  return #true
